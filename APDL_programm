!   Commands inserted into this file will be executed just prior to the ANSYS SOLVE command.
!   These commands may supersede command settings set by Workbench.

!   Active UNIT system in Workbench when this object was created:  Metric (mm, kg, N, s, mV, mA)
!   NOTE:  Any data that requires units (such as mass) is assumed to be in the consistent solver unit system.
!                See Solving Units in the help system for more information.


!ИСХОДНЫЕ УСЛОВИЯ
rr=0.000				!радиус вектор эксцентриситета
deltr=0.0000008			!шаг по эксцентриситету
theta=0				!угол эксцентриситета, град
num_of_IEXC=6			!число итераций изменения эксцентриситета
r=0.032					!радиус вала
Patm=1e5				!атмосферное давлениеи
end_of_spring=r+0.003		!конец пружинок


ALF=0.5				!коэффициент релаксации по давлению
max_iter=1				!максимальное число итераций
deltmax=0.0005			!максимальная невязка по зазору
popravka=0.0001			!поправка на идеализированную модель


razmer=0.001				!размер конечного элемента
dlinna=0.06				!длина подшипника


CLOCAL,11,cylin,0,0,0,0,0,0	!создать цилиндрическую СК с заданными эксцентриситетами
RSYS,11			!сделать цилиндрическую СК активной

!РЕШЕНИЕ
*GET,num_of_element,ELEM,0,count	!число элементов на выбранной поверхности
*VGET,number_of_elem,elem,,elist	!номера выбранных элементов

cmsel,s,mesto_pressure          !выбор области приложения давления (верхней поверхности лепестков соприк. с газом)
cmsel,u,line_no_force           !выбор области где не должно прикладываться давление

esln,s,1				 !выбор элементов связанных с выбранными узлами
esel,r,ename,,181		!выбор только элеметнов shell_181

*GET,num_of_element_small,ELEM,0,count	!число элементов на выбранной поверхности
*VGET,number_of_elem_small,elem,,elist	!номера выбранных элементов

NSLE,r						!выбор узлов принадлежащих элементам
*GET,number_node_of_element,NODE,0,count	!число узлов на выбранной поверхности
*VGET,node_of_element,node,,nlist		!номера выбранных узлов

*GET,num_of_element,ELEM,0,count	!число элементов на выбранной поверхности
*VGET,number_of_elem,elem,,elist	!номера выбранных элементов

!объявление массивов узлов для каждого элемент
*dim,pol1,array,num_of_element_small
*dim,pol2,array,num_of_element_small
*dim,pol3,array,num_of_element_small
*dim,pol4,array,num_of_element_small
*dim,Press,array,num_of_element_small


allsel,all

*GET,all_element,ELEM,0,count	!число всех элементов
*GET,all_node,NODE,0,count		!число всех узлов

allsel,all				!сброс выбора (выбор всех элементов)

cmsel,s,mesto_pressure          !выбор области приложения давления (верхней поверхности лепестков соприк. с газом)
cmsel,u,line_no_force           !выбор области где не должно прикладываться давление


*GET,num_of_sel,NODE,0,count    !число узлов на выбранной поверхности
*dim,Force,array,num_of_sel	!силы на выбранных узлах(то что считается в С#)

*dim,number_of_node,array,num_of_sel    !объявление массива выбранных координат лепестка x
*dim,coord_loc,array,num_of_sel,4        !объявление массива выбранных координат лепестка x
*dim,new_coordxn,array,num_of_sel,2 !объявление 2мерный массив с данными узлов/координат узлов X с эксцентриситетом
*dim,zazorP,array,num_of_sel        !объявление вспомогательного массива для невязок
*dim,num_num,array,all_element      !объявление массива номер узла/номер узла
*dim,after_Sharp,array,num_of_sel,2     !объявление 2мерный массив с данными узлов/координат узлов X

*dim,def1,array,num_of_sel      !объявление массива величин деформаций
*dim,deform,array,num_of_sel,2      !объявление 2мерный массив с данными узлов/зазоров
*dim,zazor,array,num_of_sel,2       !объявление 2мерный массив с данными узлов/зазоров

*VGET,number_of_node,node,,nlist	!Создание массива номеров выбранных узлов


!получение координат лепестка X для определённых узлов
*do,i,1,num_of_sel
	*get,coord_loc(i,1),node,number_of_node(i),loc,x
	*get,coord_loc(i,2),node,number_of_node(i),loc,y
		!перевод шкалы градусов из системы -180/180 в систему 0-360
		*if,coord_loc(i,2),LT,0,then
		coord_loc(i,2)=360+coord_loc(i,2)
		*else
		coord_loc(i,2)=coord_loc(i,2)
		*endif
	*get,coord_loc(i,3),node,number_of_node(i),loc,z
	coord_loc(i,4)=number_of_node(i)
*enddo

!Присвоение локальным индекса массива узлов глобальных номеров узлов
*do,i,1,num_of_sel
	num_num(coord_loc(i,4))=i
*enddo


allsel,all
cmsel,s,name_of_number      	!выбор торцевых рёбер лепестков соприкосающихся с газом
cmsel,u,line_no_force	      	!выбор области где не должно прикладываться давление (нижние элементы стыков)
nsle,r					!выбор всех узлов из предыдущих именных наборов
*VGET,one_line_number,node,,nlist	!номера выбранных узлов
*GET,num_of_line,NODE,0,count	!число узлов на выбранной линии
*dim,coord_locy_line,array,num_of_line	!объявление массива числа узлов по окружности
!получение координат Y лепестка для определённых узлов
*do,i,1,num_of_line
	*get,coord_locy_line(i),node,one_line_number(i),loc,y
		!перевод шкалы градусов из системы -180/180 в систему 0-360
		*if,coord_locy_line(i),LT,0,then
		coord_locy_line(i)=360+coord_locy_line(i)
		*else
		coord_locy_line(i)=coord_locy_line(i)
		*endif
*enddo
!Создание 2 мерного массива номер узла/координатаY
*dim,coordyn1,array,num_of_line,2		!объявление 2мерный массив с данными узлов/координат узлов Y
*do,i,1,num_of_line
	coordyn1(i,1)=number_of_node(i)
	coordyn1(i,2)=coord_locy_line(i)
*enddo

*VFUN,one_line_vozr,asort,coordyn1(1,2)	!упорядочивание массива значении координат y узлов по возрастанию

!Вычисление локального номера узла по одной окружности
max_iter_z=nint(dlinna/razmer)+1			!максимальное число элементов по длине
max_iter_y=num_of_line				!общее число элементов по окружности
delta_y=360/max_iter_y				!шаг между соседними узлами в одной окружности


*dim,mass_y,array,max_iter_y,max_iter_z,	!инициализация массива координата z/номер элемента по окружности

!Вычисление массива элементы по z -> угол элемента
*do,i,1,num_of_sel
	!nz=nint(coord_loc(i,3)/razmer)	!номер элемента по оси z
       !цикл по выбора номера элемента по окружности
	*do,k,1,max_iter_y
		*if,coord_loc(i,2),LT,one_line_vozr(k)+delta_y/3,and,coord_loc(i,2),GT,one_line_vozr(k)-delta_y/3,then
	iy=k
	nz=nint(coord_loc(i,3)/razmer)
		*endif
	*enddo
        mass_y(iy,nz+1)=i			!массив номер по z, номер по углу, номер элемента
*enddo


allsel,all
/prep7
csys,1
RSYS,11							!сделать цилиндрическую СК активной
et,100000,combin14						! COMBIN14 spring-damper element
keyopt,100000,3,0						! Longitudinal spring damper element (UX DOF)
r,100000,36800						! Spring constant
gof=48 !пока пусть 48 гофров всего по кругу
num_node_spr_okr=num_of_line/gof !10
zzz=all_node

*dim,exel,array,gof
exel(1)=1,5,9,13,17,21,25,29,34,39,
exel(11)= 43,47,51,55,59,63,67,71,75,79
exel(21)=84,89,93,97,101,105,109,113,117,121
exel(31)=125,129,134,139,143,147,151,155,159,163
exel(41)=167,171,175,179,184,189,193,197

!разворот системы координат узлов в локальную СК
nrotat,all



*do,kk,1,max_iter_z
	*do,i,1,gof
		zzz=zzz+1
		n,zzz,end_of_spring,coord_loc(mass_y(exel(i),kk),2),(kk-1)*razmer
		d,zzz,ux,0
		d,zzz,uy,0
		d,zzz,uz,0
		d,zzz,rotx,0
		d,zzz,roty,0
		d,zzz,rotz,0
		type,100000
		real,100000
		nrotat,all
		e,zzz,Coord_loc(mass_y(exel(i),kk),4)
	*enddo
*enddo

*dim,pribavka,array,num_of_IEXC

pribavka(1)=deltr*2

!инициализация массива давлений
*dim,p,array,num_of_sel,2			!объявление 2мерный массив с данными узлов/зазоров


!Инициализация начального давления
*do,i,1,num_of_element
 Press(i)=10000
*enddo

!ЦИКЛ ПО ИЗМЕНЕНИЮ ЭКЦЕНТРИСИТЕТА
*do,M,1,num_of_IEXC
rr=rr+deltr

*cfopen,'D:\bearing\BBEEAARRIINNGG\!Bearing\Start_parameters',txt
*vwrite,rr,deltr,theta,r,ALF,dlinna
(' 'G10.4,' 'G10.4,' 'G10.4,' 'G10.4,' 'G10.4,' 'G10.4,)
*cfclos

!Создание 2 мерного массива номер узла/координата X с эксцентриситетом
*do,i,1,num_of_sel
	new_coordxn(i,1)=coord_loc(i,4)
!	new_coordxn(i,2)=sqrt(r**2-rr*sin(coord_locy(i)+theta)**2)+rr*cos(coord_locy(i)+theta)
	new_coordxn(i,2)=r+rr*cos((coord_loc(i,4)+theta)*3.14/180)
*enddo





!ЦИКЛ ПО ИЗМЕНЕНИЮ ДАВЛЕНИЯ
*do,k,1,max_iter
allsel,all
/solu

!приложение силы к выбранным узлам в повёрнутой СК
*do,i,1,num_of_element_small
	SFE,number_of_elem(i),,Pres,1,Press(i)
*enddo

allsel,all
solve

/post1
set,last					!выбор данных последнего шага нагружения
CLOCAL,11,cylin,ux,uy,uz,0,0,0		!создать цилиндрическую СК с заданными эксцентриситетами
RSYS,11					!сделать цилиндрическую СК активной

!получение перемещений лепестка для определённых узлов
*do,i,1,num_of_sel
	*get,def1(i),node,coord_loc(i,4),U,x
*enddo

!Создание 2 мерного массива номер узла/перемещение
*do,i,1,num_of_sel
	deform(i,1)=coord_loc(i,4)
	deform(i,2)=def1(i)
*enddo

*cfopen,'D:\bearing\BBEEAARRIINNGG\!Bearing\Number_node_coord1',txt
*vwrite,Coord_loc(1,4),Coord_loc(1,1),Coord_loc(1,2),Coord_loc(1,3),deform(1,2)
(' 'F15.0,' 'E10.5,' 'E10.5,' 'E10.5,' 'E10.5,)
*cfclos

/sys, D:\bearing\BBEEAARRIINNGG\!Bearing\Charp_mesh_apdl.exe

*vread,Force,'D:\bearing\BBEEAARRIINNGG\!Bearing\Pressure_CSharp',txt,,
(' 'E10.5,)
*cfclos

*cfopen,'D:\bearing\BBEEAARRIINNGG\!Bearing\Test_CSharp',txt
*vwrite,Force(1,1)
(' 'E10.5,)
*cfclos


!Вычисление номеров узлов для элемента и расчёт давления для элемента
*do,i,1,num_of_element_small
	*get,pol1(i),elem,number_of_elem(i),node,	1
	*get,pol2(i),elem,number_of_elem(i),node,	2
	*get,pol3(i),elem,number_of_elem(i),node,	3
	*get,pol4(i),elem,number_of_elem(i),node,	4
	Press(i)=(Force(num_num(pol1(i)))+Force(num_num(pol2(i)))+Force(num_num(pol3(i)))+Force(num_num(pol4(i))))/4
*enddo

*cfopen,'D:\bearing\BBEEAARRIINNGG\!Bearing\Test_Pressure',txt
*vwrite,Press(1,1)
(' 'E10.5,)
*cfclos


*enddo !Давление
*enddo !Эксцентриситет





